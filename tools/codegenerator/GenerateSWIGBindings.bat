rem @ECHO OFF

SET cur_dir=%CD%

SET base_dir=%cur_dir%\..\..
SET groovy_dir=%base_dir%\lib\groovy
SET generator_dir=%base_dir%\tools\codegenerator
SET bin_dir=%cur_dir%\..\BuildDependencies\bin
SET swig_dir=%cur_dir%\%1
SET gen_subdir=generated
SET doxy_subdir=%gen_subdir%\doxygenxml

SET languages=(python javascript)
FOR %%l IN %languages% DO (
  rem go into xbmc/interfaces/<language>
  cd "%1\..\%%l"

  rem make sure all necessary directories exist and delete any old generated files
  IF NOT EXIST "%gen_subdir%" md "%gen_subdir%"
  IF EXIST "%gen_subdir%\%2.xml" del "%gen_subdir%\%2.xml"
  IF EXIST "%gen_subdir%\%2.cpp" del "%gen_subdir%\%2.cpp"
  IF NOT EXIST "%doxy_subdir%" md "%doxy_subdir%"

  rem run doxygen
  "%bin_dir%\doxygen\doxygen.exe" > NUL 2>&1

  rem run swig to generate the XML used by groovy to generate the language bindings
  "%bin_dir%\swig\swig.exe" -w401 -c++ -outdir "%gen_subdir%" -o "%gen_subdir%\%2.xml" -xml -I"%base_Dir%\xbmc" -xmllang python "%swig_dir%\%2.i"

  rem run groovy to generate the language bindings
  java.exe -cp "%groovy_dir%\groovy-all-1.8.4.jar;%groovy_dir%\commons-lang-2.6.jar;%generator_dir%;%cur_dir%\%1\..\%%l" groovy.ui.GroovyMain "%generator_dir%\Generator.groovy" "%gen_subdir%\%2.xml" "%%lSwig.cpp.template" "%gen_subdir%\%2.cpp" "%doxy_subdir%"

  rem delete the XML file generated by SWIG as it's not needed anymore
  del "%gen_subdir%\%2.xml" > NUL
  rem delete the generated doxygen files
  rmdir "%doxy_subdir%" /S /Q > NUL

  rem go back to the initial directory
  cd "%cur_dir%"
)